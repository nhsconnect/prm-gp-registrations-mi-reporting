index="$index$"
| eval _time=strptime(registrationEventDateTime,"%Y-%m-%dT%H:%M:%S")
| sort 0 _time
| streamstats earliest(_time) as conversationStart by conversationId
| where conversationStart >= strptime("$report_start$", "%Y-%m-%d") AND conversationStart <= strptime("$report_end$", "%Y-%m-%d")
| spath output=integration_outcome path=payload{}.integration.outcome
| eval ehrRequestDateTime = if(eventType="EHR_REQUEST",_time, null())
| eval ehrResponseDateTime = if(eventType="EHR_RESPONSE",_time, null())
| spath output=errors path=payload.error{}.errorCode
```| eval hasErrors = if (errors == "{}",0,1)```
| transaction conversationId keeporphans=t
| eval isIntegrated = if(eventType="EHR_INTEGRATIONS", 1, 0)
| eval readyToIntegrate = if(eventType="READY_TO_INTEGRATE_STATUSES", 1, 0)
| eval ehrRequest = if (eventType=="EHR_REQUEST", 1 ,0)
| eval ehrResponse = if(eventType="EHR_RESPONSE", 1, 0)
| eval twentyFourHoursAgo = relative_time(now(),"-24h")
| eval ehrResponseWithin24Hours = if(ehrResponse ==1 AND twentyFourHoursAgo < ehrResponseDateTime , 1, 0)
| eval twentyMinsAgo = relative_time(now(),"-20m")
| eval ehrRequestWithin20Mins = if(ehrRequest ==1 AND twentyMinsAgo < ehrRequestDateTime , 1, 0)
| eval registrationStatus = case(    
    isIntegrated == 1, "INTEGRATED",
    readyToIntegrate == 1 AND ehrRequest ==0 AND ehrResponse == 0, "READY_TO_INTEGRATE",
    readyToIntegrate == 1 AND ehrRequest ==1  AND ehrResponse == 1, "EHR_SENT",
    readyToIntegrate == 1 AND ehrResponse == 0 AND ehrRequest == 1 AND ehrRequestWithin20Mins == 1, "EHR_REQUESTED",
    readyToIntegrate == 1 AND ehrResponse == 0 AND ehrRequest == 1 AND ehrRequestWithin20Mins == 0, "SLOW_EHR_REQUESTED",
    1=1, "NOT_INTEGRATED")
| eval outcome = case(    
    isIntegrated == 0 AND readyToIntegrate == 1 AND ehrRequest ==0 AND ehrResponse == 0, "AWAITING_INTEGRATION",
    integration_outcome = "REJECTED", "REJECTED",
    integration_outcome = "INTERNAL_TRANSFER", "INTERNAL",
    integration_outcome = "FAILED_TO_INTEGRATE", "TECHNICAL_FAILURE",
    isIntegrated == 0 AND readyToIntegrate == 1 AND ehrResponse == 1 AND ehrResponseWithin24Hours == 1, "IN_PROGRESS",
    isIntegrated == 0 AND readyToIntegrate == 1 AND ehrResponse == 1 AND ehrResponseWithin24Hours == 0, "TECHNICAL_FAILURE",    
    isIntegrated == 0 AND readyToIntegrate == 1 AND ehrResponse == 0 AND ehrRequest == 1 AND ehrRequestWithin20Mins == 1,"IN_PROGRESS",
    isIntegrated == 0 AND readyToIntegrate == 1 AND ehrResponse == 0 AND ehrRequest == 1 AND ehrRequestWithin20Mins == 0,"TECHNICAL_FAILURE",
    1=1, "SUCCESS")
| eval outcomeAndStatus = mvzip(outcome, registrationStatus)
| stats dc(conversationId) by outcomeAndStatus
| rex field=outcomeAndStatus "(?<outcome>.*),(?<registration_status>.*)"
| rename dc(conversationId) as count
| table outcome, registration_status, count, errors, hasErrors
