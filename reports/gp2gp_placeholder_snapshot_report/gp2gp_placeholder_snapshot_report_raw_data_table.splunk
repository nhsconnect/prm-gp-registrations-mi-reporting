index="$index$"

{% from 'gp2gp_placeholder_snapshot_report/gp2gp_placeholder_snapshot_report_base.splunk' import
gp2gp_placeholder_report_snapshot_base %}

{{ gp2gp_placeholder_report_snapshot_base() }}


| WHERE numPlaceholders > 0

| spath output=clinicalType path=payload.ehr.placeholders{}.clinicalType
| spath output=generatedBy path=payload.ehr.placeholders{}.generatedBy
| spath output=originalMimeType path=payload.ehr.placeholders{}.originalMimeType
| spath output=reason path=payload.ehr.placeholders{}.reason

| eval soft_zip = mvzip(clinicalType, 
                        mvzip(generatedBy, 
                            mvzip(originalMimeType, reason)
                        )
                    )
| mvexpand soft_zip

| rex field=soft_zip "^(?<clinical_type>[^,]+),(?<generated_by>[^,]+),(?<original_mime_type>[^,]+),(?<reason>.*)$"
| fields - soft_zip

| eval conversation_id = conversationId
| eval total_number_of_placeholders = numPlaceholders
| eval reporting_system_supplier = reportingSystemSupplier
| eval requesting_supplier_name = requestingSupplierName
| eval sending_supplier_name = sendingSupplierName
| eval requesting_practice_ods_code = requestingPracticeOdsCode
| eval sending_practice_ods_code =  sendingPracticeOdsCode

| fillnull value="N/A"
| stats
    count as count_of_clinical_type_and_reason BY conversation_id,
                                                    total_number_of_placeholders,
                                                    clinical_type,
                                                    generated_by,
                                                    original_mime_type,
                                                    reason,
                                                    requesting_supplier_name,
                                                    sending_supplier_name,
                                                    requesting_practice_ods_code,
                                                    sending_practice_ods_code
| table conversation_id,
        total_number_of_placeholders,
        clinical_type,
        reason,
        count_of_clinical_type_and_reason,
        generated_by,
        original_mime_type,
        requesting_supplier_name,
        sending_supplier_name,
        requesting_practice_ods_code,
        sending_practice_ods_code