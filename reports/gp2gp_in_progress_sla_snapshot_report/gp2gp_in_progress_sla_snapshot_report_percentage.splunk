index="$index$"

{% from 'gp2gp_in_progress_sla_snapshot_report/gp2gp_in_progress_sla_snapshot_report_base.splunk' import
gp2gp_in_progress_sla_snapshot_base %}

{{ gp2gp_in_progress_sla_snapshot_base() }}

| eventstats count(eval(outcome!="Not eligible for electronic transfer")) as total_number_of_eligible_transfers

| eventstats count(eval(totalTransferTimeOutsideSla24Hours==0
                        and totalEhrSendingOutsideSla==0
                        and totalEhrRequestingOutsideSla==0
                        and outcome=="In progress"
                        )) as "in_flight_count"
| eventstats count(eval(totalTransferTimeOutsideSla24Hours==1
                        and outcome!="Technical failure"
                        )) as "broken_24hr_sla_count"
| eventstats count(eval(totalEhrSendingOutsideSla==1
                        and outcome!="Technical failure"
                        )) as "broken_ehr_sending_sla_count"
| eventstats count(eval(totalEhrRequestingOutsideSla==1
                        and outcome!="Technical failure"
                        )) as "broken_ehr_requesting_sla_count"

| eval in_flight_percentage = round((in_flight_count/total_number_of_eligible_transfers) * 100,2)
| eval broken_24hr_sla_percentage =  round((broken_24hr_sla_count/total_number_of_eligible_transfers) * 100,2)
| eval broken_ehr_sending_sla_percentage = round((broken_ehr_sending_sla_count/total_number_of_eligible_transfers) * 100,2)
| eval broken_ehr_requesting_sla_percentage = round((broken_ehr_requesting_sla_count/total_number_of_eligible_transfers) * 100,2)

| stats 
    first(in_flight_percentage) as in_flight_percentage, 
    first(broken_24hr_sla_percentage) as broken_24hr_sla_percentage, 
    first(broken_ehr_sending_sla_percentage) as broken_ehr_sending_sla_percentage, 
    first(broken_ehr_requesting_sla_percentage) as broken_ehr_requesting_sla_percentage

| eval label=mvappend("In flight", "Broken 24hr sla", "Broken ehr sending sla", "Broken ehr requesting sla")
| eval count=mvappend(in_flight_percentage, broken_24hr_sla_percentage, broken_ehr_sending_sla_percentage, broken_ehr_requesting_sla_percentage)

| eval soft_zip = mvzip(label, count)
| mvexpand soft_zip
 
| rex field=soft_zip "(?<label>.*),(?<count>.*)"
| table label, count
